do ->
  # $resourceをインジェクション
  app = angular.module('MyRoutesApp', ['ngResource', 'ngRoute', 'leaflet-directive'])

  # Rails CSRFトークンを使用するように設定
  app.config ['$httpProvider', ($httpProvider) ->
    $httpProvider.defaults.headers.common['X-CSRF-Token'] = $('meta[name=csrf-token]').attr('content');
    $httpProvider.defaults.headers.common['X-Requested-With'] = "XMLHttpRequest"
  ]

  # ルーティングの設定
  app.config ['$routeProvider', ($routeProvider) ->
    $routeProvider.when('/', {
      templateUrl: 'list.html',
      controller: 'ListController'
    }).when('/route/new', {
      templateUrl: 'route_detail.html',
      controller: 'RouteDetailController'
    }).when('/route/:id', {
      templateUrl: 'route.html',
      controller: 'RouteController'
    }).when('/route/:id/edit', {
      templateUrl: 'route_detail.html',
      controller: 'RouteDetailController'
    }).otherwise({
      redirectTo: '/'
    })
  ]

  # Routesテーブルに対するRESTfullのマッパーを定義
  app.factory('Routes', ['$resource', ($resource) ->
    $resource('/routes/:id.json', {id: '@id'}, {
      update: {method: 'PUT'}
    })
  ])

  # Coordinatesテーブルに対するRESTfullのマッパーを定義
  app.factory('Coordinates', ['$resource', ($resource) ->
    $resource('/coordinates/:id.json', {id: '@id'})
  ])

  # 一覧画面のコントローラ
  app.controller('ListController', ['$scope', 'Routes',
    ($scope, Routes) ->
      $scope.routes = Routes.query()
  ])

  # 地図表示画面のコントローラ
  app.controller('RouteController', ['$scope', '$routeParams', 'Routes',
    ($scope, $routeParams, Routes) ->
      angular.extend($scope, {
        center: {
          lat: 35.65889587036083,
          lng: 139.75466072559357,
          zoom: 12
        },
        paths: {
          p1: {
            color: '#FF9966',
            weight: 5,
            latlngs: []
          }
        },
        defaults: {
          scrollWheelZoom: false
        }
      })
      $scope.route = Routes.get({id: $routeParams.id}, ->
        if $scope.route.coordinates? and $scope.route.coordinates.length > 0
          $scope.center.lat = +$scope.route.coordinates[0].latitude
          $scope.center.lng = +$scope.route.coordinates[0].longitude
        $scope.paths.p1.latlngs = ({lat: +i.latitude, lng: +i.longitude} for i in $scope.route.coordinates)
      )
  ])

  # ルート編集画面のコントローラ
  app.controller('RouteDetailController', ['$scope', '$routeParams', 'Routes', 'Coordinates',
    ($scope, $routeParams, Routes, Coordinates) ->
      angular.extend($scope, {
        center: {
          lat: 35.65889587036083,
          lng: 139.75466072559357,
          zoom: 12
        },
        paths: {
          p1: {
            color: '#FF9966',
            weight: 5,
            latlngs: []
          }
        },
        defaults: {
          scrollWheelZoom: false
        }
      })

      if $routeParams?.id?
        $scope.route = Routes.get({id: $routeParams.id}, ->
          if $scope.route.coordinates? and $scope.route.coordinates.length > 0
            $scope.center.lat = +$scope.route.coordinates[0].latitude
            $scope.center.lng = +$scope.route.coordinates[0].longitude
          $scope.paths.p1.latlngs = ({lat: +i.latitude, lng: +i.longitude} for i in $scope.route.coordinates)
        )
        $scope.prevUrl = "/#/route/#{$routeParams.id}"
      else
        $scope.route = new Routes({
          title: '',
          description: '',
          coordinates: []
        })
        $scope.prevUrl = '/#/'
# TODO モデルを単数形にする
      $scope.register = ->
        if $scope.route.id?
          $scope.route.$update(->
            alert('done.')
          )
        else
          $scope.route.$save(->
            alert('done')
          )
  ])